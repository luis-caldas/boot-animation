// Outside variables
numberImages = %NUM_IMAGES%;
passwordMessage = "%PASS_MSG%";
bulletCharacter = "%BULLET_CHAR%";
passwordTyping = "%PASSWORD_TYPING%";
questionPrompt = "%QUESTION_PROMPT%";
logoMessage = "%LOGO_MESSAGE%";

// Screen size
screen.w = Window.GetWidth(0);
screen.h = Window.GetHeight(0);
screen.half.w = Window.GetWidth(0) / 2;
screen.half.h = Window.GetHeight(0) / 2;
screen.quarter.top = Window.GetHeight(0) / 4;

// Text increase
textIncrease = 8;

// Reduction sizes
topReduction = 3;
bottomReduction = 2;

// Question prompt
question = null;
answer = null;

// Message
message = null;

// Password prompt
bullets = null;
prompt = null;
now = null;
bulletImage = Image.Text(bulletCharacter, 1, 1, 1);
passwordImage = Image.Text(passwordTyping, 1, 1, 1);

// Flow
status = "play";

//-------------------//
// Background Colour //
//-------------------//

Window.SetBackgroundTopColor (0, 0, 0);
Window.SetBackgroundBottomColor (0, 0, 0);

//------------------//
// Check Which Mode //
//------------------//

if (Plymouth.GetMode() != "suspend" && Plymouth.GetMode() != "resume") {

    // Check if we are doing a text top or an image
    if (logoMessage == "") {
        // Get the top image
        logo.image = Image("top.png");
    } else {
        // Set the text
        logo.image = Image.Text(logoMessage, 1, 1, 1);
        // Blow it up
        logo.image = logo.image.Scale(logo.image.GetWidth() * textIncrease, logo.image.GetHeight() * textIncrease);
    }

    // Scale image
    logo.image = logo.image.Scale(logo.image.GetWidth() / topReduction, logo.image.GetHeight() / topReduction);

    // Create sprite
    logo.sprite = Sprite();

    // Set sprite position
    logo.sprite.SetX(Window.GetX() + (screen.half.w - logo.image.GetWidth() / 2));
    logo.sprite.SetY(Window.GetY() + (screen.quarter.top - logo.image.GetHeight() / 2));

    // Load image
    logo.sprite.SetImage(logo.image);

}

//---------------------------//
// Refresh Spinner Animation //
//---------------------------//

// Cycle through all the images and add them to the list
for (i = 1; i <= numberImages; i++) {
    bottom.images[i - 1] = Image("bottom" + i + ".png");
    bottom.images[i - 1] = bottom.images[i - 1].Scale(bottom.images[i - 1].GetWidth() / bottomReduction, bottom.images[i - 1].GetHeight() / bottomReduction);
}

// Create the sprite for the spinner
bottom.sprite = Sprite();

// Set the position of the images in the center of the screen
bottom.sprite.SetX(Window.GetX() + (screen.half.w - bottom.images[0].GetWidth() / 2));
bottom.sprite.SetY(Window.GetY() + (screen.half.h - bottom.images[0].GetHeight() / 2));

// Counter for the loading progress
progress = 0;

//------------------//
// Refresh Function //
//------------------//

fun refresh_callback() {

    // Only show spinner if not in password mode
    if (status == "play") {

        // Set the image
        bottom.sprite.SetOpacity(1);
        bottom.sprite.SetImage(bottom.images[progress]);

        // Update the progress
        progress++;
        progress %= numberImages;

    } else {

        // Hide the image
        bottom.sprite.SetOpacity(0);

    }

}
Plymouth.SetRefreshFunction (refresh_callback);

//-----------------//
// Question Prompt //
//-----------------//

fun DisplayQuestionCallback(prompt, entry) {

    // Pause the animation
    status = "pause";

    // Reset variables
    question = null;
    answer = null;

    // Change empty question
    if (entry == "")
        entry = questionPrompt;

    // Create the question
    question.image = Image.Text(prompt, 1, 1, 1);
    question.sprite = Sprite(question.image);
    question.sprite.SetX(screen.half.w - question.image.GetWidth() / 2);
    question.sprite.SetY(screen.h - 4 * question.image.GetHeight());

    // Show the answer
    answer.image = Image.Text(entry, 1, 1, 1);
    answer.sprite = Sprite(answer.image);
    answer.sprite.SetX(screen.half.w - answer.image.GetWidth() / 2);
    answer.sprite.SetY(screen.h - 2 * answer.image.GetHeight());

}
Plymouth.SetDisplayQuestionFunction(DisplayQuestionCallback);

//-----------------//
// Password Prompt //
//-----------------//

fun DisplayPasswordCallback(nil, bulletCount) {

    // Pause the animation
    status = "pause";

    // Create the Image
    prompt.image = Image.Text(passwordMessage, 1, 1, 1);
    prompt.sprite = Sprite(prompt.image);
    prompt.sprite.SetX(screen.half.w - prompt.image.GetWidth() / 2);
    prompt.sprite.SetY(screen.h - 4 * prompt.image.GetHeight());

    // Checking if we are showing dots or not
    if (passwordTyping == "") {

        // Calculate the position and size of the typing area
        totalWidth = bulletCount * bulletImage.GetWidth();
        startPos = screen.half.w - totalWidth / 2;

        // Clear all bullets (user might hit backspace)
        bullets = null;
        // Iterate the bullets and create them
        for (i = 0; i < bulletCount; i++) {
            bullets[i].sprite = Sprite(bulletImage);
            bullets[i].sprite.SetX(startPos + i * bulletImage.GetWidth());
            bullets[i].sprite.SetY(screen.h - 2 * bulletImage.GetHeight());
        }

    } else {

        // Calculate the position and size of the typing area
        startPos = screen.half.w - passwordImage.GetWidth() / 2;

        // Clear the variable
        now = null;
        // Check if the password is being typed
        if (bulletCount >= 1) {

            // Print only if required
            now.sprite = Sprite(passwordImage);
            now.sprite.SetX(startPos * passwordImage.GetWidth());
            now.sprite.SetY(screen.h - 2 * passwordImage.GetHeight());

        }

    }

}
Plymouth.SetDisplayPasswordFunction(DisplayPasswordCallback);

//----------------//
// Normal Display //
//----------------//

fun DisplayNormalCallback() {

    // Reset everything to normal
    status = "play";
    bullets = null;
    prompt = null;
    message = null;
    question = null;
    answer = null;

}
Plymouth.SetDisplayNormalFunction(DisplayNormalCallback);

//--------------//
// Show Message //
//--------------//

fun MessageCallback(text) {

    // Simply show the message
    message.image = Image.Text(text, 1, 1, 1);
    message.sprite = Sprite(message.image);
    message.sprite.SetPosition(screen.half.w - message.image.GetWidth() / 2, message.image.GetHeight());

}
Plymouth.SetMessageFunction(MessageCallback);

