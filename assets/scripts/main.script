
// Outside variables
number_of_images = %NUM_IMAGES%
password_message = "%PASS_MSG%"
bullet_character = "%BULLET_CHAR%"

// Screen size
screen.w = Window.GetWidth(0);
screen.h = Window.GetHeight(0);
screen.half.w = Window.GetWidth(0) / 2;
screen.half.h = Window.GetHeight(0) / 2;

// Question prompt
question = null;
answer = null;

// Message
message = null;

// Password prompt
bullets = null;
prompt = null;
bullet.image = Image.Text(bullet_character, 1, 1, 1);

// Flow
status = "play";

//-------------------//
// Background Colour //
//-------------------//

Window.SetBackgroundTopColor (0, 0, 0);
Window.SetBackgroundBottomColor (0, 0, 0);

//------------------//
// Check Which Mode //
//------------------//

if (Plymouth.GetMode() != "suspend" && Plymouth.GetMode() != "resume") {
  logo = ImageNew("top.png");
}

//---------------------------//
// Refresh Spinner Animation //
//---------------------------//

// Cycle through all the images and add them to the list
for (i = 0; i < number_of_images; i++)
  images[i] = Image("bottom" + i + ".png");
sprite_now = Sprite();

// Set the position of the images in the center of the screen
sprite_now.SetX(Window.GetX() + (Window.GetWidth(0) / 2 - images[0].GetWidth() / 2));
sprite_now.SetY(Window.GetY() + (Window.GetHeight(0) / 2 - images[0].GetHeight() / 2));

// Counter for the loading progress
progress = 0;

//------------------//
// Refresh Function //
//------------------//

fun refresh_callback() {

  // Only show spinner if not in password mode
  if (status == "play") {

    // Set the image
    sprite_now.SetOpacity(1);
    sprite_now.SetImage(images[progress]);

    // Update the progress
    progress++;
    progress %= number_of_images;

  } else {

    // Hide the image
    sprite_now.SetOpacity(0);

  }

}
Plymouth.SetRefreshFunction (refresh_callback);

//-----------------//
// Question Prompt //
//-----------------//

fun DisplayQuestionCallback(prompt, entry) {
    question = null;
    answer = null;

    if (entry == "")
        entry = "<answer>";

    question.image = Image.Text(prompt, 1, 1, 1);
    question.sprite = Sprite(question.image);
    question.sprite.SetX(screen.half.w - question.image.GetWidth() / 2);
    question.sprite.SetY(screen.h - 4 * question.image.GetHeight());

    answer.image = Image.Text(entry, 1, 1, 1);
    answer.sprite = Sprite(answer.image);
    answer.sprite.SetX(screen.half.w - answer.image.GetWidth() / 2);
    answer.sprite.SetY(screen.h - 2 * answer.image.GetHeight());
}
Plymouth.SetDisplayQuestionFunction(DisplayQuestionCallback);

//-----------------//
// Password Prompt //
//-----------------//

fun DisplayPasswordCallback(nil, bulletCount) {
    global.status = "pause";
    totalWidth = bulletCount * bullet.image.GetWidth();
    startPos = screen.half.w - totalWidth / 2;

    prompt.image = Image.Text(password_message, 1, 1, 1);
    prompt.sprite = Sprite(prompt.image);
    prompt.sprite.SetX(screen.half.w - prompt.image.GetWidth() / 2);
    prompt.sprite.SetY(screen.h - 4 * prompt.image.GetHeight());

    // Clear all bullets (user might hit backspace)
    bullets = null;
    for (i = 0; i < bulletCount; i++) {
        bullets[i].sprite = Sprite(bullet.image);
        bullets[i].sprite.SetX(startPos + i * bullet.image.GetWidth());
        bullets[i].sprite.SetY(screen.h - 2 * bullet.image.GetHeight());
    }
}
Plymouth.SetDisplayPasswordFunction(DisplayPasswordCallback);

//----------------//
// Normal Display //
//----------------//

fun DisplayNormalCallback() {
    global.status = "play";
    bullets = null;
    prompt = null;
    message = null;
    question = null;
    answer = null;
}
Plymouth.SetDisplayNormalFunction(DisplayNormalCallback);

//--------------//
// Show Message //
//--------------//

fun MessageCallback(text) {
    message.image = Image.Text(text, 1, 1, 1);
    message.sprite = Sprite(message.image);
    message.sprite.SetPosition(screen.half.w - message.image.GetWidth() / 2, message.image.GetHeight());
}
Plymouth.SetMessageFunction(MessageCallback);

